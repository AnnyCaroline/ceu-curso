#include "random.ceu"

emit FRAMES_SET(yes);

code/await Player(var int up, var int right, var int down, var int left, var Color color) -> NEVER do
    var Point pt = val Point(0,0);
    var int x_axis = 0;
    var int y_axis = 0;

    par do
        var int key;
        every key in KEY_PRESS do
            //atualiza x_axis e y_axis com 1 ou -1
            if (key == left) then
                x_axis = -1;
            else/if (key == right) then
                x_axis = 1;
            else/if (key == up) then
                y_axis = 1;
            else/if (key == down) then
                y_axis = -1;
            end 
        end
    with
        var int key;
        every key in KEY_UNPRESS do
            //atualiza x_axis e y_axis com 0
            if (key == left) or (key == right) then
                x_axis = 0;
            else/if (key == up) or (key == down) then
                y_axis = 0;
            end 
        end        
    with
        every FRAMES_UPDATE do
            //atualiza pt com base em x_axis e y_axis
            if (x_axis == 1) and (pt.x < 25) then
                pt.x = pt.x + 1;
            else/if (x_axis == -1) and (pt.x > -25) then
                pt.x = pt.x - 1;
            end

            if (y_axis == 1) and (pt.y < 25) then
                pt.y = pt.y + 1;
            else/if (y_axis == -1) and (pt.y > -25) then
                pt.y = pt.y - 1;
            end
        end
    with
        every FRAMES_REDRAW do
            //desenha pixel
            emit GRAPHICS_SET_COLOR_NAME(color);
            emit GRAPHICS_DRAW_PIXEL(pt.x, pt.y);
        end
    end
end

code/await Pixel (none) -> none do
    var Point pt = call Random_Point();

    do finalize with
        emit GRAPHICS_SET_COLOR_NAME(COLOR_BLACK);
        emit GRAPHICS_DRAW_PIXEL(pt.x,pt.y);
    end

    par/or do
        every FRAMES_REDRAW do
            emit GRAPHICS_SET_COLOR_NAME(COLOR_RED);
            emit GRAPHICS_DRAW_PIXEL(pt.x, pt.y);
            
            await 300ms;

            emit GRAPHICS_SET_COLOR_NAME(COLOR_YELLOW);
            emit GRAPHICS_DRAW_PIXEL(pt.x, pt.y);

            await 300ms;
        end
    with
        await MOUSE_CLICK;
    end
end

spawn Player(KEY_UP, KEY_RIGHT, KEY_DOWN, KEY_LEFT, COLOR_BLUE);
spawn Player(KEY_w , KEY_d    , KEY_s   , KEY_a   , COLOR_RED);

pool[] Pixel pixels;

every 1s do
   spawn Pixel() in pixels;
end

await FOREVER;